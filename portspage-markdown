#!/usr/bin/perl -w

our $version = "1.0.0";

########################################################################
#
# portspage-markdown (https://github.com/crshd/portspage-markdown)
#
# forked from:
# portspage (http://www.karsikkopuu.net/crux/scripts/)
#
# ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
# This is a script for generating CRUX port listings.
# Distributed under the terms of the GPL license.
# Report bugs and suggestions at https://github.com/crshd/portspage-markdown.
#
########################################################################

use strict;

our %options =
(
 timestamp_accuracy => 1,
 date_from_file => 0,
 path => 0,
);

sub print_usage
{
	print <<EOT;
Usage: portspage [OPTION]... [DIRECTORY]

  --title=TITLE               set the page title
  --header=FILE               name of file to insert before port listing
  --footer=FILE               name of file to insert after port listing
  --timestamp-accuracy=LEVEL  0 = no timestamp, 1 = date only, 2 = date and time
                              default is 1
  --date-from-file            take date from newest file instead of directory
  --date-from-pkgfile         take date from Pkgfile instead of directory
  --path                      path to ports
  --version                   output version information and exit

Report bugs to <jukka\@karsikkopuu.net>.
EOT
}

sub parse_args
{
	foreach my $arg (@ARGV)
	{
		if ($arg =~ /^--header=(.*)$/)
		{
			$options{header} = $1;
		}
		elsif ($arg =~ /^--footer=(.*)$/)
		{
			$options{footer} = $1;
		}
		elsif ($arg =~ /^--title=(.*)$/)
		{
			$options{title} = $1;
		}
		elsif ($arg =~ /^--timestamp-accuracy=(0|1|2)$/)
		{
			$options{timestamp_accuracy} = $1;
		}
		elsif ($arg =~ /^--date-from-file$/)
		{
			$options{date_from_file} = 1;
		}
		elsif ($arg =~ /^--date-from-pkgfile$/)
		{
			$options{date_from_pkgfile} = 1;
		}
		elsif ($arg =~ /^--path=(.*)$/)
		{
			 $options{path} = $1;
		}
		elsif ($arg =~ /^--version$/)
		{
			print "$version\n";
			exit 0;
		}
		elsif ($arg =~ /^--help$/)
		{
			print_usage();
			exit 0;
		}
		else
		{
			$options{directory} = $arg;
		}
	}
}

sub recurse_tree
{
	my $path = shift;
	my @list;

	while ($path =~ s/\/\//\//g) {}
	$path =~ s/\/$//;

	opendir(DIR, $path) or return;
	ENTRY:
	foreach my $entry(sort(readdir(DIR)))
	{
		next ENTRY if $entry eq ".";
		next ENTRY if $entry eq "..";
		push (@list, "$path/$entry") if -f "$path/$entry";
		push (@list, recurse_tree("$path/$entry")) if -d "$path/$entry";
	}

	return @list;
}

sub parse_pkgfile
{
	my %parsed;
	my $pkgfile = shift;

	if (open (FILE, $pkgfile))
	{
		while (<FILE>)
		{
			if ($_ =~ /^#\s*(.*?):\s*(.*)$/)
			{
				my $key = $1;
				my $value = $2;
				$value =~ s/</&lt;/g;
				$value =~ s/>/&gt;/g;
				$value =~ s/&/&amp;/g;
				$parsed{$key} = $value;
			}
			elsif ($_ =~ /^version=(.*)$/)
			{
				$parsed{version} = $1;
			}
			elsif ($_ =~ /^release=(.*)$/)
			{
				$parsed{release} = $1;
			}
		}
		close (FILE);
	}

	return { %parsed };
}

sub main
{
	my %db;

	parse_args();

	if (!$options{directory})
	{
		print_usage();
		return 0;
	}

	foreach my $file (recurse_tree($options{directory}))
	{
		if ($file =~ q:.*/(.*)/Pkgfile$:)
		{
			$db{$1} = parse_pkgfile("$file");
		}
	}

	if ($options{title})
	{
		 print "# $options{title}\n";
	}

	if ($options{header})
	{
		open(FILE, $options{header}) or die "Couldn't open header file";
		while (<FILE>)
		{
			print "  " . $_;
		}
		close(FILE);
	}

	print "Port | Version | Description |";
	$options{timestamp_accuracy} > 0 ? print " Modified |\n" : print "\n";
	print ":-|:-|:-|";
	$options{timestamp_accuracy} > 0 ? print ":-:|\n" : print "\n";
	my $count = 0;
	foreach my $port (sort keys %db)
	{
		$count++;
		$db{$port}{URL} ? print "[$port]($db{$port}{URL})" : print "$port";
		if ($options{path})
		{
			print " | [$db{$port}{version}-$db{$port}{release}]($options{path}/$port/)";
		}
		else
		{
			print " | [$db{$port}{version}-$db{$port}{release}]($options{directory}/$port/)";
		}
		$db{$port}{Description} ? print " | $db{$port}{Description} " : print " | ";

		if ($options{timestamp_accuracy} > 0)
		{
			my $date;

			if ($options{date_from_file})
			{
				my @files = recurse_tree("$options{directory}/$port");
				my @dates;
				foreach my $file (@files)
				{
					push (@dates, (stat($file))[9]);
				}
				@dates = sort @dates;
				$date = $dates[$#dates];

			}
			elsif ($options{date_from_pkgfile})
			{
				$date = (stat("$options{directory}/$port/Pkgfile"))[9];
			}
			else
			{
				$date = (stat("$options{directory}/$port"))[9];
			}

			print " | " . isotime($date, $options{timestamp_accuracy});
		}

		print "\n";
	}
	print "**$count ports**\n";

	if ($options{footer})
	{
		open(FILE, $options{footer}) or die "Couldn't open footer file";
		while (<FILE>)
		{
			print "  " . $_;
		}
		close(FILE);
	}

	print "_Generated by [portspage-markdown](https://github.com/crshd/portspage-markdown) $version on " . isotime() . "._\n";

	return 0;
}

sub isotime
{
	my $time = (shift or time);
	my $accuracy = (shift or 2);
	my @t = gmtime ($time);
	my $year = $t[5] + 1900;
	my $month = sprintf("%02d", $t[4] + 1);
	my $day = sprintf("%02d", $t[3]);

	if ($accuracy == 1)
	{
		return "$year-$month-$day";
	}

	return "$year-$month-$day " . sprintf("%02d:%02d:%02d UTC", $t[2], $t[1], $t[0]);
}

exit(main());

# End of file
